Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкаКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкаКомпоновки, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	КоличествоСТрок = ДокументРезультат.ВысотаТаблицы;
	КоличествоКолонок = ДокументРезультат.ШиринаТаблицы;

	Для СтрокаДокумента = 1 По КоличествоСТрок Цикл
		Для КолонкаДокумента = 1 По КоличествоКолонок Цикл
			Область = ДокументРезультат.Область(СтрокаДокумента, КолонкаДокумента);
			//Область.Примечание.Текст = Область.Имя;
			ИдРасшифровки = Область.Расшифровка;
			Если ТипЗнч(ИдРасшифровки) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
				ПоляРасшифровки = ДанныеРасшифровки.Элементы.Получить(ИдРасшифровки).ПолучитьПоля();
				ПолеКартинка = ПоляРасшифровки.Найти("Картинка");
				Если ПолеКартинка <> Неопределено Тогда
					ХранилищеДопИнфы = ПолеКартинка.Значение;
					Если ТипЗнч(ХранилищеДопИнфы) = Тип("СправочникСсылка.ХранилищеДополнительнойИнформации") Тогда
						Картинка = Новый Картинка(ХранилищеДопИнфы.Данные);
						
						КартинкаВОтчете = ДокументРезультат.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
						КартинкаВОтчете.Картинка = Картинка;
						КартинкаВОтчете.РазмерКартинки = РазмерКартинки.Пропорционально;
						КартинкаВОтчете.Расположить(Область);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла
	КонецЦикла;

КонецПроцедуры